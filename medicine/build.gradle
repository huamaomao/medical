apply plugin: 'com.android.application'
apply plugin: 'idea'
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.ruoer.doctor"
        compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
        buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs{
        myConfig {
            storeFile file("../signed/keystore.jks")
            storePassword "doctor...."
            keyAlias "doctor"
            keyPassword "doctor...."
        }
    }

    lintOptions {
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        disable 'InvalidPackage'
    }
    dexOptions {
        preDexLibraries = false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles 'proguard-project.txt'
            signingConfig signingConfigs.myConfig
        }
        debug {
            proguardFiles 'proguard-project.txt'
            //minifyEnabled true
            signingConfig signingConfigs.myConfig
        }
    }
    productFlavors {
        channel {
            manifestPlaceholders = [ channel:"ruoer" ]
        }
    }
    channels()

}
// 多渠道打包
def channels() {
    if (project.hasProperty("channel")) {
        // 渠道号配置文件路径
        def path = "./build-types/channels.txt"
        file(path).eachLine { line ->
            if(!line.startsWith("//")){ //剔除注释行
                android.buildTypes.create(line, {
                    manifestPlaceholders = [ channel:line ]
                    signingConfig android.buildTypes.release.signingConfig
                    minifyEnabled android.buildTypes.release.minifyEnabled
                    proguardFiles android.buildTypes.release.proguardFiles
                })
            }
        }
    }
}

dependencies {
    /********jar***********/
    compile 'com.alibaba:fastjson:1.1.41'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.jakewharton:butterknife:6.1.0'
}

